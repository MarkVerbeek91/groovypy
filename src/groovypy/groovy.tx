
GroovyFile:
    // attrs = Attrs
    pipeline = Pipeline
;

Attrs:
    Node | Pipeline
;

Node:
    'node' '{' '}'
;

Pipeline:
    'pipeline'
    '{'
     agent = Agent?
     trigger = Triggers?
     stages = Stages?
     post = Post?
     '}'
;

Agent:
    'agent' '{' 'label' data=STRING '}'
;

Triggers:
    'triggers'
    '{'
    cron = Cron?
    '}'
;

Cron:
    'cron' '(' period=STRING ')'
;

Stages:
    'stages' '{'
    stage *= Stage
     '}'
;

Post:
    'post' '(' name=STRING ')'
    '{'
    cleanups = Cleanup?
    '}'
;

Stage:
    'stage' '(' name=STRING ')'
    '{'
    condition = Condition?
    steps *= StageContent
    '}'
;

Condition:
    'when'
    '{'
    expression = Expression
    '}'
;

Expression:
    'expression'
    '{'
        data *= Data
    '}'
;

Data:
    ID ID '(' STRING ')' '.' ID '(' STRING ')'
;

StageContent:
    Parallel | Steps | Stages
;

Parallel:
    'parallel'
    '{'
    stage *= Stage
    '}'
;

Steps:
    'steps'
    '{'
        commands *= Command
    '}'
;

Cleanup:
    'always'
    '{'
    commands *= Command
    '}'
;

Command:
    Echo | Shell
;

Echo:
    'echo' message=STRING
;

Shell:
    'sh' command=STRING
;
